import java.util.concurrent.TimeUnit


buildscript {
    repositories {
        gradlePluginPortal()
        mavenLocal()
        maven { url "https://nexus.inductiveautomation.com/repository/public/" }
        maven { url "https://nexus.inductiveautomation.com/repository/inductiveautomation-thirdparty/" }
        maven { url "https://nexus.inductiveautomation.com/repository/inductiveautomation-releases/" }
        maven { url "https://nexus.inductiveautomation.com/repository/inductiveautomation-snapshots/" }
        mavenCentral()
        google()

    }

    ext.sdk_version = "8.1.0"

    dependencies {
        classpath("com.inductiveautomation.gradle:ignition-module-plugin:1.2.10-SNAPSHOT")
    }
}

plugins {
    id "base"
}

version "1.0.3"
group "org.imdc"


allprojects {
    apply plugin: "ignition-module-plugin"

    ignitionModule {
        // name of the .modl file to build
        fileName "PerspectiveMomentaryButton"

        // what is the name of the "root" gradle project for this module.  In this case, it"s "this", aka, the project
        // specified as <repoPath>/perspective-component/build.gradle
        moduleRoot "perspective-momentary-button"

        // module xml configuration
        moduleName "Perspective Momentary Button"
        moduleId "org.imdc.perspective.momentarybutton"
        moduleVersion "${project.version}"
        moduleDescription "The module provides a Perspective momentary button component"
        requiredIgnitionVersion "8.1.0"
        requiredFrameworkVersion "8"
        isFree true
        license "license.html"
        moduleDependencies = [
                [scope: "G", moduleId: "com.inductiveautomation.perspective"],
                [scope: "D", moduleId: "com.inductiveautomation.perspective"]
        ]
        // map our projects to the scopes their jars should apply.  Web isn't here because its assets are moved
        // into the gateway resource folder as part of the module's build
        projectScopes = [
                [name: "gateway", scope: "G"],
                [name: "designer", scope: "D"],
                [name: "common", scope: "GD"]
        ]

        hooks = [
                [scope: "G", hookClass: "org.imdc.perspective.momentarybutton.gateway.GatewayHook"],
                [scope: "D", hookClass: "org.imdc.perspective.momentarybutton.designer.DesignerHook"]
        ]

    }

    if (project.plugins.hasPlugin("java")) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    // check for new versions of dependencies more frequently than default 24 hours.
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor(30, TimeUnit.SECONDS)
        }
    }

    // where should we try to resolve maven artifacts from?
    repositories {
        mavenLocal()
        maven { url "https://nexus.inductiveautomation.com/repository/public/" }
        maven { url "https://nexus.inductiveautomation.com/repository/inductiveautomation-thirdparty/" }
        maven { url "https://nexus.inductiveautomation.com/repository/inductiveautomation-releases/" }
        maven { url "https://nexus.inductiveautomation.com/repository/inductiveautomation-snapshots/" }
        mavenCentral()
        google()
    }
}

task deepClean() {
    dependsOn allprojects.collect { "${it.path}:clean" }
    description "Executes clean tasks and remove node plugin caches."
    doLast {
        delete(file(".gradle"))
    }
}

/**
 * Gradle wrapper configuration*/
wrapper {
    distributionUrl = "https://services.gradle.org/distributions/gradle-6.9.4-all.zip"
}
